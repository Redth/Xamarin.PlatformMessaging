plugins {
    id 'com.android.library'
    id 'maven-publish'
}


android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    libraryVariants.all {
        variant -> variant.outputs.all {
            outputFileName = "xamarin-platform-messaging.aar"
        }
    }
}

dependencies {
}


// Because the components are created only during the afterEvaluate phase, you must
// configure your publications using the afterEvaluate() lifecycle method.
afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release

                // You can then customize attributes of the publication as shown below.
                groupId = 'com.xamarin'
                artifactId = 'platformmessaging'
                version = '0.1.0'

                pom {
                    name = 'Xamarin Platform Messaging'
                    description = 'Messaging Bridge API for Xamarin <-> Android'
                    url = 'https://github.com/redth/Xamarin.PlatformMessaging'
                    licenses {
                        license {
                            name = "The MIT License"
                            url = "http://www.opensource.org/licenses/MIT"
                        }
                    }
                    developers {
                        developer {
                            id = 'redth'
                            name = 'Redth'
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com/redth/Xamarin.PlatformMessaging.git'
                        url = 'http://github.com/redth/Xamarin.PlatformMessaging'
                    }
                }
            }
        }
    }
}
